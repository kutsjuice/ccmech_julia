{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 50,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Info    : Reading 'c:\\git_pro\\ccmech_julia\\lesson_4\\geometry.msh'...\n",
      "Info    : 45 entities\n",
      "Info    : 2586 nodes\n",
      "Info    : 13688 elements\n",
      "Info    : Done reading 'c:\\git_pro\\ccmech_julia\\lesson_4\\geometry.msh'\n"
     ]
    }
   ],
   "source": [
    "tcf(filename::String) = (@__DIR__) * \"\\\\\" * filename;\n",
    "\n",
    "using Gridap\n",
    "using GridapGmsh\n",
    "\n",
    "msh_file = \"geometry.msh\" |> tcf;\n",
    "\n",
    "model = GmshDiscreteModel(msh_file);\n",
    "\n",
    "vtk_file = \"geometry\" |> tcf;\n",
    "writevtk(model, vtk_file);"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Для анализа расcчитаем собственные клебания прямоугольной балки $40мм\\times 25мм \\times 240мм$ выполненного из алюминия ($E=70ГПа$, $\\mu=0{,}33$, $\\rho=2800\\frac{кг}{м^3}$). Закрепление осуществляется за часть нижней грани по площадке длиной $40мм$, имитируя клеевое соединение. Поверхность закрепления показана на рисунке.\n",
    "\n",
    "<center><img src=\"beam.png\" width=\"1000\"/></center>\n",
    "<center>КЭ-модель и помеченой границой Дирихле</center>"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 51,
   "metadata": {},
   "outputs": [],
   "source": [
    "const E = 70.0e9\n",
    "const ν = 0.33\n",
    "const λ = (E*ν)/((1+ν)*(1-2*ν))\n",
    "const μ = E/(2*(1+ν))\n",
    "const ρ = 2.8e3\n",
    "σ(ε) = λ*tr(ε)*one(ε) + 2*μ*ε\n",
    "\n",
    "mm = 1e-3;\n",
    "l_full = 200mm;\n",
    "l_fixed = 40mm;\n",
    "w = 40mm;\n",
    "h = 25mm;\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 52,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "GenericMeasure()"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "degree = 2\n",
    "Ω = Triangulation(model)\n",
    "dΩ = Measure(Ω,degree)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 53,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "TrialFESpace()"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "reffe = ReferenceFE(lagrangian,VectorValue{3,Float64},1)\n",
    "V = TestFESpace(model,reffe,conformity=:H1,dirichlet_tags = [\"fixed\"])\n",
    "\n",
    "g0 = VectorValue(0.0,  0.0, 0.0)\n",
    "# g1 = VectorValue(0.0, 0.0)\n",
    "\n",
    "U = TrialFESpace(V, [g0])"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Уравнение динамики\n",
    "\n",
    "Ранее были рассмотрены задачи статики, которые сводились к решению уравнению вида\n",
    "$$\n",
    "\\mathbf{Ku}=\\mathbf{f}\n",
    "$$\n",
    "В данном уравнении учитывается только восстанавливающая сила $\\mathbf{F}_r(\\mathbf{u}) = \\mathbf{Ku}$\n",
    "\n",
    "В случае, если задача нестационарная, необходимо также учитывать инерционные и\n",
    "$$\n",
    "    \\mathbf{F}_i(\\mathbf{u}, \\dot{\\mathbf{u}}, \\ddot{\\mathbf{u}}) = \\mathbf{M}\\ddot{\\mathbf{u}}\n",
    "$$\n",
    "и диссипативные силы\n",
    "$$\n",
    "    \\mathbf{F}_d(\\mathbf{u}, \\dot{\\mathbf{u}}, \\ddot{\\mathbf{u}}) = \\mathbf{C}\\dot{\\mathbf{u}}\n",
    "$$\n",
    "\n",
    "Тогда полное уравнение динамики примет вид:\n",
    "$$\n",
    "    \\mathbf{M}\\ddot{\\mathbf{u}} + \\mathbf{C}\\dot{\\mathbf{u}} + \\mathbf{Ku}=\\mathbf{f}_{o}(t)\n",
    "$$\n",
    "Данная система является системой диффференциальных уравнений второго порядка.\n",
    "\n",
    "При анализе собственных частот исследуют так называемые \"свободные\"  колебания - колебания без воздействия внешней силы $\\left(\\mathbf{f}_{o}(t) = 0 \\right)$, возникающие при некоторых начальных условиях $\\left(\\mathbf{u}(0) = \\mathbf{u}_0; \\: \\dot{\\mathbf{u}}(0) = \\mathbf{v}_0; \\right)$. Частоты, на которых система колеблется при отсутствии внушних сил, наываются **собственными частотам** (СЧ). Кроме того, из экспериментальных работ известно, что контрукционное демпфирование обычно довольно мало, и оказывает незначительное влияние на СЧ. Поэтому исследование собственных колебаний начинаю с исследования консервативной системы, т.е. без наличия диссипативных сил:\n",
    "$$\n",
    "    \\mathbf{M}\\ddot{\\mathbf{u}} + \\mathbf{Ku}= 0\n",
    "$$. \n",
    "Решение такого уравнения будем искать в виде\n",
    "$$\n",
    "    \\mathbf{u} = \\mathbf{u}_0 \\sin(\\omega t + \\phi_0)\n",
    "$$\n",
    "\n",
    "Если подставить даннове решение в предыдущее, то получится следующая система:\n",
    "$$\n",
    "    -\\omega^2\\mathbf{M}\\mathbf{u}_0 \\sin(\\omega t + \\phi_0) + \\mathbf{K} \\mathbf{u}_0 \\sin(\\omega t + \\phi_0)= 0\n",
    "$$\n",
    "Если отбросить тривиальные решения $\\left( \\mathbf{u}_0 = 0\\right)$, то для решения системы необходимо обращения в нуль определителья матрицы\n",
    "$$\n",
    "\\det\\left( \\mathbf{K} -\\omega^2\\mathbf{M}\\right) = 0\n",
    "$$\n",
    "\n",
    "Если ввести замену $\\lambda = \\frac{1}{\\omega^2}$ и поделить на матрицу жесткости, то задача сведется к отысканию таких $\\omega$ и $\\mathbf{u}_0$, при которых выполняется следующее условие:\n",
    "\n",
    "$$\n",
    "    \\mathbf{K}^{-1} \\mathbf{M} \\mathbf{u}_0 = \\lambda \\mathbf{u}_0\n",
    "$$\n",
    "Данная задача носит название **проблемы собственных значений** и как извествено имеет N решений, где N - размерность матриц жесткости и масс. Иначе говоря, у системы имеется N СЧ и N векторов $\\mathbf{u}_0$, которые задают форму согласно с которой осущестляетс деформация при колебаниях на данной СЧ. Эти вектора называются **собственными формами** или просто **формами** колебаний.\n",
    "\n",
    "Для решения используется собственное разложение, позволяюще представить матрицу в виде произведениея\n",
    "$$\n",
    "\\mathbf{K}^{-1}\\mathbf{M} = \\mathbf{V}\\mathbf{\\Lambda}\\mathbf{V}^T\n",
    "$$\n",
    "где $\\mathbf{V}$ - матрица собственных векторов, столбцами которой являются собственные формы\n",
    "$$\n",
    "    \\mathbf{V} = \\left[ \n",
    "        \\begin{matrix}\n",
    "            | & | & | &&|\\\\\n",
    "            \\mathbf{u}_0^1 & \\mathbf{u}_0^2 & \\mathbf{u}_0^3 & \\dots & \\mathbf{u}_0^N\\\\ \n",
    "            | & | & | &&|\\\\\n",
    "        \\end{matrix}\n",
    "    \\right]\n",
    "$$\n",
    "a $\\mathbf{\\Lambda}$ - матрица собственных значений\n",
    "$$\n",
    "     \\mathbf{\\Lambda} = \\left[ \n",
    "        \\begin{matrix}\n",
    "            \\lambda_1 & 0 & 0 & \\dots & 0\\\\\n",
    "            0 & \\lambda_2 & 0 & \\dots & 0\\\\\n",
    "            0 & 0 & \\lambda_3 & \\dots & 0\\\\\n",
    "            \\vdots & \\vdots & \\vdots & \\ddots & \\vdots\\\\ \n",
    "            0 & 0 & 0 & \\dots & \\lambda_N\\\\\n",
    "        \\end{matrix}\n",
    "    \\right]\n",
    "$$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Составление ансамбля\n",
    "Отличием исследования на собственные частоты, от статического анализа, является то, что необходимо составить две билененых формы и соответствующих им оператора - для матрицы жесткости и для матрицы масс.\n",
    "\n",
    "Сборка матрицы жесткости не отсличается от того, как она  осуществлялась ранее.\n",
    "Слабая форма для матрицы масс выводится из условия минимизации потенциальной энергии выглядит следующим образом:\n",
    "$$\n",
    "    \\rho\\int_{\\Omega}^{}{ \\mathbf{u} \\cdot \\mathbf{v} d\\Omega}\n",
    "$$\n",
    "И хотя линейная форма в данном случае не  требуется, т.к. внешнии силы отсутствуют и решение системы как таковое не оосуществляется, она необхоимо для процедуры сборки, поэтому объявим ее как нулевую."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 54,
   "metadata": {},
   "outputs": [],
   "source": [
    "bfK(u,v) = ∫( ε(v) ⊙ (σ∘ε(u)) )*dΩ\n",
    "bfM(u,v) = ρ*∫(v⋅u)dΩ\n",
    "L(v) = 0; #∫(dot(f, v))*dΩ\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 55,
   "metadata": {},
   "outputs": [],
   "source": [
    "op_K = AffineFEOperator(bfK, L, U, V);\n",
    "mK = get_matrix(op_K);\n",
    "\n",
    "op_M = AffineFEOperator(bfM, L, U, V);\n",
    "mM = get_matrix(op_M);"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "После того как мы получили глобальные матрицы жесткости $\\mathbf{K}$ и масс $\\mathbf{M}$ можем определить выполнить собственное разложение матрицы $\\mathbf{T} = \\mathbf{K}^{-1}\\mathbf{M} = \\mathbf{V}\\mathbf{\\Lambda}\\mathbf{V}^T$\n",
    "\n",
    "> **_Примечание:_**\n",
    ">\n",
    "> В дествительности, операция собственного разложения для произведения матриц вида $\\mathbf{B}^{-1}\\mathbf{A}$ настолько распространенная операция, что для ее вычисления существет специальный алгоритм. Все дело в том, что матрицы, получаемые в результате сборки задачи МКЭ являются ***разреженными*** (см. [описание](https://ru.wikipedia.org/wiki/%D0%A0%D0%B0%D0%B7%D1%80%D0%B5%D0%B6%D0%B5%D0%BD%D0%BD%D0%B0%D1%8F_%D0%BC%D0%B0%D1%82%D1%80%D0%B8%D1%86%D0%B0)), для которых существуют отдельные высокоэффективные алгоритмы, и в том числе, для их спектрального разложения. Но при обращении  произвольной разреженной матрицы зачастую получается ***плотная*** матрица, алгоритмы работы с которыми гораздо менее эффективны.\n",
    "\n",
    "Для нашего примера, расчитаем лишь первые 10 собственных частот и форм, для чего воспользуемся функцией `eigs` из библиотеки `Arpack` позволяющей эффективно вычислять собственное и сингулярное разложение для больших разряженных матриц."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 56,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(ComplexF64[1.1064860501896258e-7 + 0.0im, 4.990199333712628e-8 + 0.0im, 3.1718433706365113e-9 + 0.0im, 2.2228032296601015e-9 + 0.0im, 1.6410820465815979e-9 + 0.0im, 8.17595790975238e-10 + 0.0im, 4.4977285155180636e-10 + 0.0im, 2.8228099661041386e-10 + 0.0im, 2.4602733814296367e-10 + 0.0im, 1.4727554159928213e-10 + 0.0im], ComplexF64[0.0029965017525555747 + 0.0im 0.005094898257728452 + 0.0im … -0.004435292351014197 + 0.0im 0.01296421174642135 + 0.0im; -5.9277616954598813e-5 + 0.0im -0.040562654868438526 + 0.0im … -0.017280949268611533 + 0.0im -0.0005974990994266303 + 0.0im; … ; -1.1250618692903352e-5 + 0.0im -0.00025095296192511734 + 0.0im … -0.005716813360236916 + 0.0im -2.443314341145208e-5 + 0.0im; 0.00029161514697378894 + 0.0im 3.447299266765575e-5 + 0.0im … -0.0007440353740537074 + 0.0im -0.008954496529794267 + 0.0im], 10, 3, 33, [-1.8518304043612154e-13, -2.4568655394023134e-13, -4.770101675468574e-14, -2.8321313844331195e-13, -2.8016125614002103e-13, 1.4317172308191843e-13, 1.252444880402856e-13, -2.568678506403363e-13, -6.445956950129968e-14, 7.705921397073154e-14  …  -2.5026175426071094e-13, 8.535264783627613e-14, -2.5021376405085207e-14, 1.9402935745566963e-13, 1.3370746976451715e-13, 1.0674847987677518e-13, 1.6277747946875935e-14, 1.6348917811570813e-13, 1.8217109569355462e-13, -1.114399138084127e-14])"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "using Arpack\n",
    "\n",
    "num_eig_vectors = 10\n",
    "\n",
    "res = eigs(mM, mK; nev = num_eig_vectors, which=:LM)\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Согласно (1) угловая скорость может быть вчислена из собственного числа матрицы $\\mathbf{T}$\n",
    "$$\n",
    "    \\omega_i  = \\frac{1}{\\sqrt{\\lambda_i}}\\\\\n",
    "    \n",
    "$$\n",
    "откуда легко получить собственную частоту\n",
    "$$\n",
    "    \n",
    "    f_i = \\frac{\\omega_i}{2\\pi}\n",
    "\n",
    "$$\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 57,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "10-element Vector{Float64}:\n",
       "   478.4616792047911\n",
       "   712.461145316099\n",
       "  2825.948643321959\n",
       "  3375.7449145268743\n",
       "  3928.755341393028\n",
       "  5566.097482459644\n",
       "  7504.530254165768\n",
       "  9472.820557779283\n",
       " 10146.78475463881\n",
       " 13114.593006144285"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "\n",
    "freqs = [sqrt(1/λ) / (2π) for λ in real.(res[1])]"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "После этого можно сохранить и визуализировать все полученные формы колебаний.\n",
    "`Gridap` позволяет экспортировать несколько полей файл результатов. Для этого непобходимо создать словарь с полями, которые хотим экспортировать."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 59,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "([\"c:\\\\git_pro\\\\ccmech_julia\\\\lesson_4\\\\eigen_freq.vtu\"],)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "eigen_modes = Dict()\n",
    "for (i,(f,vec)) in enumerate(zip(freqs,eachcol(res[2])))\n",
    "    eigen_modes[\"mode_$(i)_(f=$f)\"] = FEFunction(U,real.(vec))\n",
    "end\n",
    "\n",
    "\n",
    "res_file = \"eigen_freq\" |> tcf;\n",
    "\n",
    "writevtk(Ω, res_file ,  cellfields=eigen_modes)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Сравнение с аналитической моделью\n",
    "\n",
    "Аналитически собственные частоты изгибных колебаний в плоскости $YZ$ консольно заделаной балки можно расчитать по следующей формуле:\n",
    "$$\n",
    "    f_i = k_i^2 \\sqrt{\\frac{E I_x}{m l^4}}\n",
    "$$\n",
    "где $k_i = \\left[1{,}875; 4{,}694; 7{,}855 \\right]$ -коэффициент формы, $m$ - погонная масса балки, $I_x$ - осевой момент инерции сечения балки, $l$ - длина балки.\n",
    "Аналогично можно рассчитать и частоты колебаний в плоскости $ZX$, для этого достаточно заменить момент инерции $I_x$ на $I_y$\n",
    "\n",
    "Отдельное внимание, следует уделить закреплению. Приведенная формула работает для закрепления когда заделка \"идеальная\". В нашем же случае закреплена лишь нижняя часть балки. С одной стороны, длина консоли (свободной части балки) составляет $l_{full} - l_{fixed} = 200мм$ и именно ее следовало подставить в формулу."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 60,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "3×2 Matrix{Float64}:\n",
       "   788.682   1261.89\n",
       "  4942.94    7908.71\n",
       " 13841.8    22146.8"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "k = [1.875, 4.694, 7.855]\n",
    "Ix = w * h^3 / 12\n",
    "Iy = h * w^3 / 12\n",
    "I = [Ix, Iy]\n",
    "m = w*h*ρ\n",
    "freq_analytical = Matrix{Float64}(undef, 3, 2)\n",
    "for i in 1:3, j in 1:2\n",
    "    freq_analytical[i,j] = k[i]^2 * sqrt(E*I[j] / (m * (l_full - l_fixed)^4)) / (2π)\n",
    "end\n",
    "freq_analytical"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Однако из рассчета видно, что полученные значения существенно выше аналогичных, полученных при помощи МКЭ.\n",
    "Действительно, если внимательно изучить 10-ю собственную форму, то видно, что часть балки, которая находится над зоной закрепления, так же подвергается деформированию, а следовательно вносит свой вклад в динамическую жествкость.\n",
    "Следовательно, использовать в расчете формальную длину консоли при таком способе закрепления не совсем корректно, и более верно было бы говорить о некой \"эффективной\" длине консоли, которая несколько больше, чем формальная.\n",
    "<center><img src=\"3rd_form.png\" width=\"1000\"/></center>\n",
    "<center>10-я собственная форма (3-я изгибная в плоскости ZX)</center>\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Задания для самостоятельной проработки\n",
    "1. Подберите эффективную длину консоли для данной задачи.\n",
    "2. Надите формулу для крутильных колебаний  консольной балки и сравните результаты. Какая погрешность получилась для крутильных СЧ?\n",
    "3. Найдите собственные частоты, соответствующие поперечным колебаниям балки (растяжение-сжатие). Объясните получившуюся форму.\n",
    "4. По имеющемуся файлу `geometry.geo` составьте код на `Julia`, которыйй генерирует такю же геометрию. Добавьте симметричную верхнуюю грань в физическую группу `\"fixed\"`. Как это повлияло на решение?"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.2",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
